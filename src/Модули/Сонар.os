///////////////////////////////////////////////////////////////////
//
// Модуль с набором методов работы с SonarQube
// Используются методы rest-api sonarqube
// (C) TheShadowCo
//
///////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////
// Программный интерфейс
///////////////////////////////////////////////////////////////////

// ПолучитьПроекты
//	Возвращает набор проектов
// Параметры:
//  АдресСервера  - Строка - Адрес (хост) сервера SonarQube
//  Токен  - Строка - Токен пользователя, от имени которого выполняются запросы к API
//
// Возвращаемое значение:
//   Соответствие   - Коллекция проектов
//		* Ключ - Строка - Код (Ключ) проекта
//		* Значение - Структура - Описание проекта
//			** Идентификатор - Строка - Идентификатор проекта
//			** Код - Строка - Код (Ключ) проекта
//
Функция ПолучитьПроекты(АдресСервера, Токен) Экспорт
	
	URLШаблон = "components/search?qualifiers=TRK&ps=500&p=%1";
	НомерСтраницы = 1;
	Проекты = Новый Соответствие();
	
	Пока Истина Цикл
		
		URL = СтрШаблон(URLШаблон, Формат(НомерСтраницы, "ЧГ="));
		Ответ = ВыполнитьЗапрос(АдресСервера, Токен, URL, "GET");
		
		Для Каждого ОписаниеПроекта Из Ответ.components Цикл

			НовыйПроект = Новый Структура();
			НовыйПроект.Вставить("Идентификатор", ОписаниеПроекта.id);
			НовыйПроект.Вставить("Код", ОписаниеПроекта.key);
			Проекты.Вставить(НовыйПроект.Код, НовыйПроект);
			
		КонецЦикла;
		
		Если БольшеНетДанных(Ответ) Тогда
			Прервать;
		КонецЕсли;

		НомерСтраницы = НомерСтраницы + 1;

	КонецЦикла;
	
	Возврат Проекты;

КонецФункции

// ПолучитьЗамечанияПроекта
//	Возвращает набор замечаний проекта по установленным отборам
// Параметры:
//  АдресСервера  - Строка - Адрес (хост) сервера SonarQube
//  Токен  - Строка - Токен пользователя, от имени которого выполняются запросы к API
//  ОписаниеПроекта  - Структура - Описание проекта SonarQube
//		* Идентификатор - Строка - Идентификатор проекта
//		* Код - Строка - Код (Ключ) проекта
//  Статусы - Массив - Массив строковых идентификаторов статусов замечаний
//  ИзEDTВКонфигуратор - Булево - Признак необходимости преобразования замечаний между родительским проектом и дочерними
//
// Возвращаемое значение:
//   Соответствие   - Коллекция замечаний
//		* Ключ - Строка - Относительный путь к файлу, в котором зафиксировано замечание
//		* Значение - Структура - Описание проекта
//			** ПутьКФайлу - Строка -  Относительный путь к файлу, в котором зафиксировано замечание
//			** Ошибки - Соответствие - Набор зарегистрированных замечаний (ошибок)
//				*** Ключ - Строка - Хэш замечания
//				*** Значение - Структура - Описание замечания
//					**** ПутьКФайлу - Строка - Относительный путь к файлу, в котором зафиксировано замечание
//					**** Код - Строка - Ключ замечания
//					**** Хэш - Строка - Хэш замечания
//
Функция ПолучитьЗамечанияПроекта(АдресСервера, Токен, ОписаниеПроекта, Статусы, ИзEDTВКонфигуратор) Экспорт
	
	Замечания = Новый Соответствие();
	
	Для Каждого Статус Из Статусы Цикл
		
		ЦиклПоПроекту(АдресСервера, Токен, Замечания, ВРег(СокрЛП(Статус)), ОписаниеПроекта.Код, ИзEDTВКонфигуратор);

	КонецЦикла;
	
	Возврат Замечания;
	
КонецФункции

// ПолучитьЗакрываемыеЗамечания
//	Возвращает набор открытых замечаний проекта, которые необходимо закрыть
// Параметры:
//  АдресСервера  - Строка - Адрес (хост) сервера SonarQube
//  Токен  - Строка - Токен пользователя, от имени которого выполняются запросы к API
//  ОписаниеПроекта  - Структура - Описание проекта SonarQube
//		* Идентификатор - Строка - Идентификатор проекта
//		* Код - Строка - Код (Ключ) проекта
//  ЗамечанияРодительскогоПроекта - Соответствие - Замечания для закрытия из родительского проекта. См. ПолучитьЗамечанияПроекта
//
// Возвращаемое значение:
//   Соответствие   - Коллекция замечаний
//		* Ключ - Строка - Хэш замечания
//		* Значение - Структура - Описание замечания
//			** ПутьКФайлу - Строка - Относительный путь к файлу, в котором зафиксировано замечание
//			** Код - Строка - Ключ замечания
//			** Хэш - Строка - Хэш замечания
//
Функция ПолучитьЗакрываемыеЗамечания(АдресСервера, Токен, ОписаниеПроекта, ЗамечанияРодительскогоПроекта) Экспорт
	
	URLШаблон = "issues/search?ps=500&statuses=OPEN,CONFIRMED,REOPENED&componentKeys=%1&p=";
	Замечания = Новый Соответствие();
	
	Для Каждого МодульСЗмечаниями Из ЗамечанияРодительскогоПроекта Цикл
		
		URL = СтрШаблон(URLШаблон, ОписаниеПроекта.Код + ":" + МодульСЗмечаниями.Ключ);
		НомерСтраницы = 1;
		Пока Истина Цикл
			
			Ответ = ВыполнитьЗапрос(АдресСервера, Токен, URL + Формат(НомерСтраницы, "ЧГ="), "GET");
			Для Каждого ОписаниеОшибки Из Ответ.issues Цикл
				
				ПутьКФайлу = СтрЗаменить(ОписаниеОшибки.component, ОписаниеПроекта.Код + ":", "");
				Хэш = ПолучитьХэшЗамечания(ОписаниеОшибки, ПутьКФайлу);
				
				Если МодульСЗмечаниями.Значение.Ошибки.Получить(Хэш) = Неопределено Тогда
					Продолжить;
				КонецЕсли;

				Хэш = ПолучитьХэшЗамечания(ОписаниеОшибки, ПутьКФайлу);
				НовоеОписаниеОшибки = НовоеОписаниеОшибки(ПутьКФайлу, ОписаниеОшибки.key, Хэш);
				Замечания.Вставить(Хэш, НовоеОписаниеОшибки);
				
			КонецЦикла;
			
			Если БольшеНетДанных(Ответ) Тогда
				Прервать;
			КонецЕсли;
			
			НомерСтраницы = НомерСтраницы + 1;
			
		КонецЦикла;

	КонецЦикла;
	
	Возврат Замечания;
	
КонецФункции

// ЗакрытьЗамечания
//	Выполняет закрытие замечаний: устанавливает признак "WONTFIX" со ссылкой на родительский проект
// Параметры:
//  АдресСервера  - Строка - Адрес (хост) сервера SonarQube
//  Токен  - Строка - Токен пользователя, от имени которого выполняются запросы к API
//  ЗакрываемыеЗамечания - Соответствие - Замечания, которые привнесены из родительского проекта и требуют закрытия. См. ПолучитьЗакрываемыеЗамечания
//  Комментарий - Строка - Текстовое сообщение, которое будет добавлено как коментарий к закрываемому замечанию
//  Теги - Строка - Список тегов, разделенных запятой, которые будут добавлены к закрываемым замечаниям.
//
Процедура ЗакрытьЗамечания(АдресСервера, Токен, ЗакрываемыеЗамечания, Комментарий, Теги = "") Экспорт
	
	URL = СтрШаблон("issues/bulk_change?do_transition=wontfix&comment=%1&issues=", Комментарий);
	URLДобавленияТегов = СтрШаблон("issues/bulk_change?add_tags=%1&issues=", Теги);

	ЗамечанияДляОбработки = Новый Массив();
	Для Каждого Замечание Из ЗакрываемыеЗамечания Цикл
		
		Если ЗамечанияДляОбработки.Количество() = 100 Тогда
			// Сначала отдельно устанавливаем теги, так как если это делать одновременно с закрытием замечаний,
			// то теги не устанавливаются.
			ВыполнитьЗапрос(АдресСервера, Токен, URLДобавленияТегов + СтрСоединить(ЗамечанияДляОбработки, ","), "POST");
			ВыполнитьЗапрос(АдресСервера, Токен, URL + СтрСоединить(ЗамечанияДляОбработки, ","), "POST");
			ЗамечанияДляОбработки.Очистить();
		КонецЕсли;
		
		ЗамечанияДляОбработки.Добавить(Замечание.Значение.Код);
		
	КонецЦикла;
	
	Если ЗамечанияДляОбработки.Количество() Тогда
		ВыполнитьЗапрос(АдресСервера, Токен, URLДобавленияТегов + СтрСоединить(ЗамечанияДляОбработки, ","), "POST");
		ВыполнитьЗапрос(АдресСервера, Токен, URL + СтрСоединить(ЗамечанияДляОбработки, ","), "POST");
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////
// Слубные процедуры и функции
///////////////////////////////////////////////////////////////////

Процедура ЦиклПоПроекту(АдресСервера, Токен, Замечания, Статус, КлючПроекта, ИзEDTВКонфигуратор)

	URLШаблон = "issues/search?ps=500&statuses=%1&componentKeys=%2&p=%3";
	НомерСтраницы = 1;

	Пока Истина Цикл
		URL = СтрШаблон(URLШаблон, Статус, КлючПроекта, Формат(НомерСтраницы, "ЧГ="));
		Ответ = ВыполнитьЗапрос(АдресСервера, Токен, URL, "GET");

		Если НомерСтраницы = 1 И Ответ.paging.total > 10000 Тогда
			ЦиклПоКаталогам(АдресСервера, Токен, Замечания, Статус, КлючПроекта, ИзEDTВКонфигуратор);
			Прервать;
		Иначе
			СобратьЗамечания(КлючПроекта, Ответ, Замечания, ИзEDTВКонфигуратор);
		КонецЕсли;
		
		Если БольшеНетДанных(Ответ) Тогда
			Прервать;
		Иначе
			НомерСтраницы = НомерСтраницы + 1;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Процедура ЦиклПоКаталогам(АдресСервера, Токен, Замечания, Статус, КлючПроекта, ИзEDTВКонфигуратор)

	КаталогиПроекта = КаталогиПроекта(АдресСервера, Токен, КлючПроекта);
	
	URLШаблон = "issues/search?ps=500&statuses=%1&componentKeys=%2&p=%3";
	
	Для каждого КаталогПроекта Из КаталогиПроекта Цикл

		НомерСтраницы = 1;
		Пока Истина Цикл
			URL = СтрШаблон(URLШаблон, Статус, КаталогПроекта, Формат(НомерСтраницы, "ЧГ="));
			Ответ = ВыполнитьЗапрос(АдресСервера, Токен, URL, "GET");

			Если НомерСтраницы = 1 И Ответ.paging.total > 10000 Тогда
				ЦиклПоФайлам(АдресСервера, Токен, Замечания, Статус, КаталогПроекта, КлючПроекта, ИзEDTВКонфигуратор);
				Прервать;
			Иначе
				СобратьЗамечания(КлючПроекта, Ответ, Замечания, ИзEDTВКонфигуратор);
			КонецЕсли;
			
			Если БольшеНетДанных(Ответ) Тогда
				Прервать;
			Иначе
				НомерСтраницы = НомерСтраницы + 1;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;

КонецПроцедуры

Процедура ЦиклПоФайлам(АдресСервера, Токен, Замечания, Статус, КлючКомпонента, КлючПроекта, ИзEDTВКонфигуратор)

	ФайлыПроекта = ФайлыПроекта(АдресСервера, Токен, КлючКомпонента);
	
	URLШаблон = "issues/search?ps=500&statuses=%1&componentKeys=%2&p=%3";
	
	Для каждого ФайлПроекта Из ФайлыПроекта Цикл

		НомерСтраницы = 1;
		Пока Истина Цикл
			URL = СтрШаблон(URLШаблон, Статус, ФайлПроекта, Формат(НомерСтраницы, "ЧГ="));
			Ответ = ВыполнитьЗапрос(АдресСервера, Токен, URL, "GET");
			СобратьЗамечания(КлючПроекта, Ответ, Замечания, ИзEDTВКонфигуратор);
			Если БольшеНетДанных(Ответ) Тогда
				Прервать;
			Иначе
				НомерСтраницы = НомерСтраницы + 1;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

Функция КаталогиПроекта(АдресСервера, Токен, КлючПроекта)

	Возврат ПодчиненныеКомпоненты(АдресСервера, Токен, КлючПроекта, "DIR");

КонецФункции

Функция ФайлыПроекта(АдресСервера, Токен, КлючКомпонента)

	Возврат ПодчиненныеКомпоненты(АдресСервера, Токен, КлючКомпонента, "FIL");

КонецФункции

Функция ПодчиненныеКомпоненты(АдресСервера, Токен, КлючКомпонента, Квалификатор)

	URLШаблон = "components/tree?ps=500&component=%1&qualifiers=%2&p=%3";
	Результат = Новый Массив;
	НомерСтраницы = 1;
	Пока Истина Цикл
		URL = СтрШаблон(URLШаблон, КлючКомпонента, Квалификатор, Формат(НомерСтраницы, "ЧГ="));
		Ответ = ВыполнитьЗапрос(АдресСервера, Токен, URL, "GET");
		Для каждого ОписаниеКомпонента Из Ответ.components Цикл
			Результат.Добавить(ОписаниеКомпонента.key);
		КонецЦикла;
		Если БольшеНетДанных(Ответ) Тогда
			Прервать;
		Иначе
			НомерСтраницы = НомерСтраницы + 1;
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;

КонецФункции

Процедура СобратьЗамечания(КлючПроекта, ОтветСервера, Замечания, ИзEDTВКонфигуратор)

	Для Каждого ОписаниеОшибки Из ОтветСервера.issues Цикл
				
		ПутьКФайлу = СтрЗаменить(ОписаниеОшибки.component, КлючПроекта + ":", "");
		Если ИзEDTВКонфигуратор Тогда
			ПутьКФайлу = СтрЗаменить(ПутьКФайлу, "configuration/src/", "src/configuration/");
			ПутьКФайлу = СтрЗаменить(ПутьКФайлу, "/ManagerModule.bsl", "/Ext/ManagerModule.bsl");
			ПутьКФайлу = СтрЗаменить(ПутьКФайлу, "/ObjectModule.bsl", "/Ext/ObjectModule.bsl");
			ПутьКФайлу = СтрЗаменить(ПутьКФайлу, "/CommandModule.bsl", "/Ext/CommandModule.bsl");
			ПутьКФайлу = СтрЗаменить(ПутьКФайлу, "/ValueManagerModule.bsl", "/Ext/ValueManagerModule.bsl");
			ПутьКФайлу = СтрЗаменить(ПутьКФайлу, "/configuration/Configuration", "/configuration/Ext");
			Если СтрНайти(ПутьКФайлу, "Forms") Тогда
				ПутьКФайлу = СтрЗаменить(ПутьКФайлу, "/Module.bsl", "/Ext/Form/Module.bsl");
			Иначе
				ПутьКФайлу = СтрЗаменить(ПутьКФайлу, "/Module.bsl", "/Ext/Module.bsl");
			КонецЕсли;
		КонецЕсли;
		
		Хэш = ПолучитьХэшЗамечания(ОписаниеОшибки, ПутьКФайлу);
		НовоеОписаниеОшибки = НовоеОписаниеОшибки(ПутьКФайлу, ОписаниеОшибки.key, Хэш);
		
		ТекущийМодуль = Замечания.Получить(ПутьКФайлу);
		Если ТекущийМодуль = Неопределено Тогда 
			ТекущийМодуль = Новый Структура("ПутьКФайлу, Ошибки", ПутьКФайлу, Новый Соответствие());
		КонецЕсли;
		ТекущийМодуль.Ошибки.Вставить(Хэш, НовоеОписаниеОшибки);
		
		Замечания.Вставить(ПутьКФайлу, ТекущийМодуль);
		
	КонецЦикла;

КонецПроцедуры

Функция БольшеНетДанных(ОтветСервера)
	
	Возврат ОтветСервера.paging.pageSize * ОтветСервера.paging.pageIndex >= ОтветСервера.paging.total;

КонецФункции

Функция ПолучитьХэшЗамечания(ОписаниеОшибки, ПутьКФайлу)
	Возврат ПутьКФайлу + ОписаниеОшибки.rule 
			+ ?(ОписаниеОшибки.Свойство("textRange"), 
				"" + ОписаниеОшибки.textRange.startLine + ОписаниеОшибки.textRange.endLine 
					+ ОписаниеОшибки.textRange.startOffset + ОписаниеОшибки.textRange.endOffset, 
				"");
КонецФункции

Функция НовоеОписаниеОшибки(ПутьКФайлу, Код, Хэш)

	Возврат Новый Структура("ПутьКФайлу, Код, Хэш", ПутьКФайлу, Код, Хэш);

КонецФункции

Функция ВыполнитьЗапрос(АдресСервера, Токен, URL, Операция) 
	
	HTTPЗапрос = Новый HTTPЗапрос;
	HTTPЗапрос.АдресРесурса = "/api/" + URL;
	HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/json");
	
	HTTP = Новый HTTPСоединение(АдресСервера, , Токен);
	Если Операция = "GET" Тогда
		ОтветHTTP = HTTP.Получить(HTTPЗапрос);
	Иначе 
		ОтветHTTP = HTTP.ОтправитьДляОбработки(HTTPЗапрос);
	КонецЕсли;
	
	Если ОтветHTTP.КодСостояния = 200 Тогда
		json = Новый ЧтениеJSON();
		json.УстановитьСтроку(ОтветHTTP.ПолучитьТелоКакСтроку());
		Возврат ПрочитатьJson(json);
	КонецЕсли;
	
	ТекстИсключения = СтрШаблон("Код ответа: %1
		|Ответ: %2
		|URL: %3",
		ОтветHTTP.КодСостояния, ОтветHTTP.ПолучитьТелоКакСтроку(), URL);
	ВызватьИсключение ТекстИсключения;
	
КонецФункции
